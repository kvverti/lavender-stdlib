' The util namespace contains miscellaneous utility functions.

@import global using _

' Returns the minimum of the given values, according to the
' given comparator.
(def minOf(vals, cmp)
    let siz(len(vals)) =>
    (def impl(min, idx)
        => min ; idx = siz
        => impl(vals(idx), idx + 1) ; cmp(vals(idx), min)
        => impl(min, idx + 1) ; 1
    )(vals(0), 0)
)

' Returns the maximum of the given values, according to the
' given comparator.
def maxOf(vals, cmp) => minOf(vals, flip(cmp))

' Returns the minimum of the given values, according to '<'.
def min(...vals) => minOf(vals, \<\)

' Returns the maximum of a and b, according to '<'.
def max(...vals) => minOf(vals, \>\)

' Performs a binary search for the given element in the given collection
' using the given comparison function. The function should implement the
' "less than" comparison, and the collection should be sorted according
' to the comparison function. Returns the index of the element if it is
' in the collection. If the element is not in the collection, returns
' '(-idx - 1)' where 'idx' is an index where the element would be if it
' were in the collection.
(def binarySearchAs(vect, elem, cmp) =>
    (def impl(lo, hi)
        let mid((lo + hi) // 2)
        => -mid - 1 ; lo >= hi
        => impl(lo, mid) ; cmp(elem, vect(mid))
        => impl(mid + 1, hi) ; cmp(vect(mid), elem)
        => mid ; 1
    )(0, len(vect))
)

' Performs a binary search using the global comparison function '<'.
' See binarySearchAs for details of the search.
def binarySearch(vect, elem) => binarySearchAs(vect, elem, \<\)

' Returns an object that represents the set defined by the given function.
' The expression 'x in SetOf(func)' will return true if and only if 'func(x)'
' returns true.
(def SetOf(func) =>
    (def obj(a)
        => __yes__ ; a = __object__
        => func ; a = \in\
    )
)

' Returns a function that tests elements against the given set. The expression
' '[Filter(set)](elem)' will return true if and only if 'elem in set' returns true.
def Filter(set) => def impl(a) => a in set

' An object representing a pair of elements.
(def Pair(a, b) =>
    (def obj(c)
        => __yes__ ; c = __object__
        => a ; c = 0
        => b ; c = 1
        => 2 ; c = \len
        => str(a) ++ " -> " ++ str(b) ; c = \str
        => (def(f) => Pair(f(a), f(b))) ; c = \map\
        => (def(id, f) => f(f(id, a), b)) ; c = \fold\
        => { a, b } ; c = \toVect\
    )
)

' Returns the curried form of the given binary function.
def curry(binaryop) => def left(a) => def right(b) => binaryop(a, b)

' Returns the uncurried form of the given curried binary function.
def uncurry(curried) => def impl(a, b) => curried(a)(b)

' Returns a function that reverses the arguments to 'binaryop', passing
' the first as the second and the second as the first.
def flip(binaryop) => def impl(a, b) => binaryop(b, a)

' An object which represents the half-open range [a..b). Ranges cannot be
' mapped or filtered, but they can be folded over. Ranges take up much
' less space compared to the equivalent vector of numbers.
(def Range(a, b) =>
    (def obj(c)
        => __yes__ ; c = __object__
        => b - a ; c = \len
        => "[" ++ str(a) ++ ".." ++ str(b) ++ ")" ; c = \str
        => (def(el) => el >= a && el < b) ; c = \in\
        => (def(id, f) => _rangeFold(a, b, f)(id, a)) ; c = \fold\
        => \obj fold ({}, \sys:cat) ; c = \toVect\
    )
)

(def _rangeFold(a, b, f) =>
    (def impl(ac, idx)
        => ac ; idx = b
        => impl(f(ac, idx), idx + 1) ; 1
    )
)

' Returns the start of the range.
def start(range) => sys:cval(range, 0)

' Returns the stop of the range.
def stop(range) => sys:cval(range, 1)
